// SPDX-License-Identifier: MIT
pragma solidity ^0.8.6;

import "@chainlink/contracts/src/v0.8/KeeperCompatible.sol";
import "@openzeppelin/contracts@4.6.0/token/ERC721/extensions/ERC721URIStorage.sol";
import "@openzeppelin/contracts@4.6.0/utils/Counters.sol";

contract SuperFrog is ERC721, ERC721URIStorage, KeeperCompatibleInterface {
    using Counters for Counters.Counter;

    Counters.Counter public tokenIdCounter;
 
   // Metadata for each stage
    string[] pinataUri = [
        "https://gateway.pinata.cloud/ipfs/QmYeUH9avraqkvBwKcDcTXoBpbFXrZmjY28Qct16xzu64V",
        "https://gateway.pinata.cloud/ipfs/QmWkJhTcZskeQpFfMS3rcbbdXnxHMb2N4wSME9qW9PxhaC",
        "https://gateway.pinata.cloud/ipfs/QmZBkCLMAgpm6SX35aP2VT56NMJpH1aTskPKuEWd8kjtNB",
        "https://gateway.pinata.cloud/ipfs/Qmcgc21r9rpgcvuojNxu9JgahZwrdMVJfV169XCS118xUW",
        "https://gateway.pinata.cloud/ipfs/QmULFRhsri5QcBmy3wfK14cJLvguFfSR3JjG87hxnTqzyp"
    ]; 

    uint256 lastTimeStamp;
    uint256 interval;

    constructor(uint _interval) ERC721("Super Frog", "SFG") {
        interval = _interval;
        lastTimeStamp = block.timestamp;
    }

    function checkUpkeep(bytes calldata /* checkData */) external view override returns (bool upkeepNeeded, bytes memory /* performData */) {
        uint256 tokenId = tokenIdCounter.current() - 1;
        bool done;
        if (frogStage(tokenId) >= 4) {
            done = true;
        }

        upkeepNeeded = !done && ((block.timestamp - lastTimeStamp) > interval);        
        // We don't use the checkData in this example. The checkData is defined when the Upkeep was registered.
    }

    function performUpkeep(bytes calldata /* performData */) external override {
        //We highly recommend revalidating the upkeep in the performUpkeep function
        if ((block.timestamp - lastTimeStamp) > interval ) {
            lastTimeStamp = block.timestamp;            
            uint256 tokenId = tokenIdCounter.current() - 1;
            growFrog(tokenId);
        }
        // We don't use the performData in this example. The performData is generated by the Keeper's call to your checkUpkeep function
    }

    function safeMint(address to) public {
        uint256 tokenId = tokenIdCounter.current();
        tokenIdCounter.increment();
        _safeMint(to, tokenId);
        _setTokenURI(tokenId, pinataUri[0]);
    }

    function growFrog(uint256 _tokenId) public {
        if(frogStage(_tokenId) >= 4){return;}
        // Get the current stage of the frog and add 1
        uint256 newVal = frogStage(_tokenId) + 1;
        // store the new URI
        string memory newUri = pinataUri[newVal];
        // Update the URI
        _setTokenURI(_tokenId, newUri);
    }

    // determine the stage of the frog growth
    function frogStage(uint256 _tokenId) public view returns (uint256) {
        string memory _uri = tokenURI(_tokenId);
        // egg
        if (compareStrings(_uri, pinataUri[0])) {
            return 0;
        }
        // tadpole
        if (compareStrings(_uri, pinataUri[1])) {
            return 1;
        }
        // walking tadpole
        if (compareStrings(_uri, pinataUri[2])) {
            return 2;
        }
        // baby frog
        if (compareStrings(_uri, pinataUri[3])) {
            return 3;
        }
        // Must be adult frog
        return 4;
    }

    function compareStrings(string memory a, string memory b) public pure
        returns (bool) {
        return (keccak256(abi.encodePacked((a))) == 
            keccak256(abi.encodePacked((b))));
    }

    // The following function is an override required by Solidity.
    function _burn(uint256 tokenId) internal
        override(ERC721, ERC721URIStorage) {
        super._burn(tokenId);
    }

    // The following function is an override required by Solidity.
    function tokenURI(uint256 tokenId) public view
        override(ERC721, ERC721URIStorage)
        returns (string memory) {
        return super.tokenURI(tokenId);
    }
}
